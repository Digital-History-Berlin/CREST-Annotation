{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# SPARQL Queries for Data Sync between Crest and a Knowledge Graph\n",
    "\n",
    "The following SPARQL queries must be set to allow data sync between Crest Annotation and the Digital Heraldry Knowledge Graph.\n",
    "\n",
    "Variables that are passed from outside the SPARQL query are set as `{{variable}}` inside the respective query.\n",
    "\n",
    "It is assumed, that the following variables can be passed between Crest and the SPARQL queries:\n",
    "- `manifestIRI`: URL of the IIIF manifest of which the current image is part of\n",
    "- `iiifImageURL`: URL of the current image file as it is given in the IIIF manifest of which the current image is part of\n",
    "- `croppedImageLink`: URL, adhearing to the IIIF image API standard, that displays onl the contents of an image annotation\n",
    "\n",
    "Note on terminology: In general, everything that pertains to the image files is organised in and represented as a IIIF manifest and is addressed as such in the following notebook. Everything addressedd as a _conceptual_ representation of something (e.g. a IIIF canvas) refers to something that is stored as an entity in the Knowledge Graph, which does not directly represent a digital image but can be linked to one. For example: A page from a book is represented conceptually in the KG as a `dhoo:ManuscriptFolio` (and may have according metadata; e.g. a specific date, material properties, …). This conceptual representation is linked to an image (=the digital scan of the page). This image file of a page from a book has a URL and is modelled as a _canvas_ inside a IIIF manifest."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Export Data from Crest into the KG\n",
    "\n",
    "Prerequisites in the KG:\n",
    "- A conceptual representation of the image collection/IIIF manifest that is exported from Crest must already exist in the KG (e.g. as `dhoo:Manuscript` or another subclass of `dhoo:Object`). This entails, that the `dhoo:Manuscript` also has already in the KG been linked to its IIIF manifest."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Check if the current IIIF manifest already exists in the KG"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "PREFIX dhor: <http://digitalheraldry.org/dho/representation#>\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "PREFIX dhoe: <http://digitalheraldry.org/dho/entity#>\n",
    "\n",
    "ASK {\n",
    "  ?manuscript a dhoo:Manuscript ;\n",
    "              dhoo:hasIIIFResource <{{manifestIRI}}> .\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Check if the current IIIF image file with a linked conceptual representation of the page/folio already exists in the KG"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "PREFIX dhor: <http://digitalheraldry.org/dho/representation#>\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "PREFIX dhoe: <http://digitalheraldry.org/dho/entity#>\n",
    "\n",
    "PREFIX iiif: <http://iiif.io/api/presentation/2#>\n",
    "PREFIX oa: <http://www.w3.org/ns/oa#>\n",
    "\n",
    "ASK {\n",
    "  ?manuscript a dhoo:Manuscript ;\n",
    "              dhoo:hasIIIFResource <{{manifestIRI}}> .\n",
    "\n",
    "  <{{manifestIRI}}> iiif:hasSequences ?sequence .\n",
    "  ?sequence rdf:rest*/rdf:first ?firstSequence .\n",
    "  ?firstSequence iiif:hasCanvases ?canvasList .\n",
    "  ?canvasList rdf:rest*/rdf:first ?canvas .\n",
    "  ?canvas rdfs:label ?canvasLabel .\n",
    "  ?canvas iiif:hasImageAnnotations ?annotations .\n",
    "  ?annotations rdf:rest*/rdf:first ?anno .\n",
    "  ?anno oa:hasBody <{{iiifImageURL}}> .\n",
    "  \n",
    "  ?manuscriptFolioIRI dhoo:hasImageFile <{{iiifImageURL}}> .\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Case 1: A representation of the image does not yet exists in the KG\n",
    "\n",
    "In this case, the representation of the image in the KG (as `dhoo:ManuscriptFolio`) has to be added as well as the annotations, that were created in Crest.\n",
    "The following branch has to be followed if the previous SPARQL ASK query returned `false`. (Otherwise, continue with _Case 2_.)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Adding the IIIF manifest into the KG is quicker by uploading the manifests' json file directly into the KGs' triplestore"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "curl -X POST -H \"Content-Type: application/ld+json\" --data-binary @{iiif_manifest_filename} {sparql_endpoint_url}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Takes an IRI `{{manifestIRI}}` of a IIIF manifest and creates a dhoo:ManuscriptFolio entity for each canvas that is in the manifest. Each dhoo:ManuscriptFolio is automatically connected to the dhoo:Object in the KG that uses the manifest IRI with dhoo:hasIIIFResource."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "# create-object-parts-from-manifest.sparql\n",
    "\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX iiif: <http://iiif.io/api/presentation/2#>\n",
    "PREFIX oa: <http://www.w3.org/ns/oa#>\n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "\n",
    "INSERT {\n",
    "    ?newObjectPartURI dhoo:partOfObject ?object .\n",
    "    ?newObjectPartURI a dhoo:ManuscriptFolio .\n",
    "    ?newObjectPartURI a owl:NamedIndividual .\n",
    "    ?newObjectPartURI dhoo:folioNumber ?canvasLabel .\n",
    "    ?newObjectPartURI dhoo:hasImageFile ?imageURL .\n",
    "}\n",
    "WHERE {\n",
    "    BIND( <{{manifestIRI}}> AS ?manifestIRI ) . \n",
    "    ?object dhoo:hasIIIFResource ?manifestIRI .\n",
    "    ?manifestIRI iiif:hasSequences ?sequence .\n",
    "    ?sequence rdf:rest*/rdf:first ?firstSequence .\n",
    "    ?firstSequence iiif:hasCanvases ?canvasList .\n",
    "    ?canvasList rdf:rest*/rdf:first ?canvas .\n",
    "    ?canvas rdfs:label ?canvasLabel .\n",
    "    ?canvas iiif:hasImageAnnotations ?annotations .\n",
    "    ?annotations rdf:rest*/rdf:first ?anno .\n",
    "    ?anno oa:hasBody ?imageURL .\n",
    "\n",
    "    # Create new URI for each object part\n",
    "    BIND( IRI(CONCAT( str(?object), \"-\", REPLACE(?canvasLabel, \" \", \"%20\") )) AS ?newObjectPartURI )\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Continue with _Case 2_"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Case 2: A representation of the image already exists in the KG\n",
    "\n",
    "In this case, only the annotations, that were created in Crest, have to be added to the KG. The following SPARQL queries ensure, that they are linked to the correct `dhoo:ManuscriptFolio` (whose image representaion is the current one in Crest)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Return the URI of the conceptual representation of the current IIIF image file. The query returns the URI as `manuscriptFolioIRI`, `canvasLabel`, as well as `manuscriptIRI` (which represents the conceptual representation of the whole IIIF manifest in the KG)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "PREFIX dhor: <http://digitalheraldry.org/dho/representation#>\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "PREFIX dhoe: <http://digitalheraldry.org/dho/entity#>\n",
    "\n",
    "PREFIX iiif: <http://iiif.io/api/presentation/2#>\n",
    "PREFIX oa: <http://www.w3.org/ns/oa#>\n",
    "\n",
    "SELECT ?manuscriptFolioIRI ?manuscriptIRI ?canvasLabel\n",
    "WHERE {\n",
    "  ?manuscriptIRI a dhoo:Manuscript ;\n",
    "           dhoo:hasIIIFResource <{{manifestIRI}}> .\n",
    "\n",
    "  <{{manifestIRI}}> iiif:hasSequences ?sequence .\n",
    "  ?sequence rdf:rest*/rdf:first ?firstSequence .\n",
    "  ?firstSequence iiif:hasCanvases ?canvasList .\n",
    "  ?canvasList rdf:rest*/rdf:first ?canvas .\n",
    "  ?canvas rdfs:label ?canvasLabel .\n",
    "  ?canvas iiif:hasImageAnnotations ?annotations .\n",
    "  ?annotations rdf:rest*/rdf:first ?anno .\n",
    "  ?anno oa:hasBody <{{iiifImageURL}}> .\n",
    "  \n",
    "  ?manuscriptFolioIRI dhoo:hasImageFile <{{iiifImageURL}}> .\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**QUESTION @Dominik:** Haben wir in Crest eine im gesamten Crest-Projekt/IIIF-Manifest eindeutige numerische ID für jede Annotation? Falls das der Fall ist, kann die uuid in der `representationIRI` im Folgenden durch eben diese numerische ID ersetzt werden. Falls es eine solche ID nicht gibt, kann alles so belassen werden."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The `representationIRI` has to be constructed outside of the SPARQL query with the following schema:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "representationIRI = manuscriptIRI.replace('http://digitalheraldry.org/dho/object#', 'http://digitalheraldry.org/dho/representation#')\n",
    "representationIRI = representationIRI + '-' + uuid4()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Create a conceptual representation of a annotation and link it to the image annotation from Crest"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "# create-representations-from-bbox-coordinates.sparql\n",
    "\n",
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "PREFIX dhor: <http://digitalheraldry.org/dho/representation#>\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "\n",
    "INSERT {\n",
    "    <{{representationIRI}}> a dhor:CoatOfArmsVisualRepresentation ;\n",
    "        a dhor:CoatOfArmsRepresentation ;\n",
    "        a owl:NamedIndividual ;\n",
    "        dhor:folioNumber \"{{canvasLabel}}\" ;\n",
    "        dhoo:locatedOnFolio <{{manuscriptFolioIRI}}> ;\n",
    "        dhor:locatedOnObject <{{manuscriptIRI}}> ;\n",
    "        dhoo:hasImageFile <{{croppedImageLink}}> .\n",
    "}\n",
    "WHERE {\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Import Data from the KG into Crest\n",
    "\n",
    "It is assumed, that in Crest a project with a IIIF manifest has already been created. This IIIF manifest is used to import the correct data from the KG."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Load all annotations for a given `manifestIRI`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "plaintext"
    }
   },
   "outputs": [],
   "source": [
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "PREFIX dhor: <http://digitalheraldry.org/dho/representation#>\n",
    "PREFIX dhoo: <http://digitalheraldry.org/dho/object#>\n",
    "PREFIX dhoe: <http://digitalheraldry.org/dho/entity#>\n",
    "\n",
    "PREFIX iiif: <http://iiif.io/api/presentation/2#>\n",
    "PREFIX oa: <http://www.w3.org/ns/oa#>\n",
    "PREFIX dhoh: <http://digitalheraldry.org/dho/heraldry#>\n",
    "\n",
    "SELECT ?folioImageFileURL ?blazon ?blazonTextAnnotation ?annotationImageFile\n",
    "WHERE {\n",
    "  ?manuscriptIRI a dhoo:Manuscript ;\n",
    "           dhoo:hasIIIFResource <{{manifestIRI}}> .\n",
    "  \n",
    "  ?manuscriptFolio dhoo:partOfObject ?manuscriptIRI ;\n",
    "                   dhoo:hasImageFile ?folioImageFileURL .\n",
    "  \n",
    "  # Load annotations\n",
    "  OPTIONAL {\n",
    "    ?coaRepresentation dhoo:locatedOnFolio ?manuscriptFolio ;\n",
    "                       dhoo:hasImageFile ?annotationImageFile .\n",
    "    \n",
    "    # Load KG entity that describes the annotation\n",
    "    OPTIONAL {\n",
    "      ?coaRepresentation dhor:hasBlazoningAct ?blazonAct .\n",
    "      ?blazonAct dhor:hasBlazon ?blazon .\n",
    "      \n",
    "      # Load textual transcription\n",
    "      OPTIONAL {\n",
    "        ?blazon dhoh:hasBlazonFromOMA ?blazonTextAnnotation .\n",
    "      }\n",
    "    }\n",
    "  }\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The coordinates of each annotation can be extracted from the IIIF URL of the respective `annotationImageFile`:\n",
    "\n",
    "https://gallica.bnf.fr/iiif/ark:/12148/btv1b55009806h/f155/**7,1100,656,764**/full/0/native.jpg"
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
